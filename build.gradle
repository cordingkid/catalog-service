plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	// 스프링 클라우드 버전
	set('springCloudVersion', "2023.0.3")
	// 테스트컨테이너 버전
	set('testcontainersVersion', "1.19.8")
	set('testKeycloakVersion', "3.3.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// 설정 서버와 통합하기 위한 스프링 클라우드 클라이언트
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	// 서버 연결 재시도 스프링 리트라이
	implementation 'org.springframework.retry:spring-retry'
	// 새로고침 이벤트를 트리거하는 의존성
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// 데이터베이스 버전 관리를 위한 플라이웨이 의존성 추가
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'


	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// PostgreSQL DB 컨테이너 관리 기능 제공
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:junit-jupiter'

	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "com.github.dasniko:testcontainers-keycloak:${testKeycloakVersion}"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}" //테스트컨테이너 의존성 관리를 위한 BOM
	}
}

bootRun {
	systemProperty 'spring.profiles.active', 'testdata'
}

// 빌드팩을 사용해 OCI 이미지를 빌드하기 위한 스프링 부트 플러그인 작업
bootBuildImage {
	builder = "docker.io/paketobuildpacks/builder-jammy-base"
	imageName = "${project.name}" // 빌드할 OCI 이미지의 이름
	environment = ["BP_JVM_VERSION" : "17.*"] // 이미지에 설치할 JVM 버전

	docker { // 컨테이너 저장소 연결을 설정하기 위한 섹션
		publishRegistry { // 컨테이너 저장소 인증을 설정하기 위한 섹션
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
